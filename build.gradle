import nu.studer.gradle.jooq.JooqEdition
import org.jooq.meta.jaxb.Logging

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.11.1'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '11.11.2'
    id 'nu.studer.jooq' version '10.1.1'
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.ngnmsn'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.webjars:bootstrap:5.3.7'
    implementation 'org.webjars.npm:bootstrap-icons:1.13.1'
    implementation 'org.webjars:jquery:3.7.1'
    implementation 'org.webjars:webjars-locator:0.52'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.4.0'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    jooqGenerator 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

flyway {
    url = 'jdbc:mysql://localhost:3306/template'
    user = 'root'
    password = 'root'
    driver = 'com.mysql.cj.jdbc.Driver'
    cleanDisabled = false
}

jooq {
    version = dependencyManagement.importedProperties['jooq.version']
    edition = JooqEdition.OSS
    configurations {
        main {
            generationTool {
                logging = Logging.WARN
                jdbc {
                    driver = flyway.driver
                    url = flyway.url
                    user = flyway.user
                    password = flyway.password
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'template'
                        excludes = 'flyway_schema_history'
                    }
                    target {
                        packageName = 'com.ngnmsn.template'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                // 除外パターン（設定クラスなど）
                '**/config/**',
                '**/Application.class',
                '**/com/ngnmsn/template/tables/**',
                '**/com/ngnmsn/template/Tables.class',
                '**/com/ngnmsn/template/Keys.class',
                '**/com/ngnmsn/template/Indexes.class'
            ])
        }))
    }
}

test {
    finalizedBy jacocoTestReport
}

generateJooq.dependsOn flywayClean
generateJooq.dependsOn flywayMigrate